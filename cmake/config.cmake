if (HXLIBS_IS_IMPORTED_BY_FIND_PACKAGE)
    message("HXLibs is imported by find_package")
    set(hxlibs_target_name "HXLibs::HXLibs")
else()
    if (NOT CMAKE_PROJECT_NAME STREQUAL "HXLibs")
        message("HXLibs is imported by fetchContent or add_subdirectory")
    endif()
    set(hxlibs_target_name "HXLibs")
endif()

# 导入第三方库 liburing
if (LINUX)
    include(cmake/includeLib/FindUring.cmake)
endif()

# OpenSSL
option(HXLIBS_ENABLE_SSL "Enable SSL support" ON)
message(STATUS "[HXLibs] HXLIBS_ENABLE_SSL: ${HXLIBS_ENABLE_SSL}")

if (HXLIBS_ENABLE_SSL)
    # 启动自动查找 OpenSSL
    find_package(OpenSSL QUIET)
    if (OpenSSL_FOUND)
        message(STATUS "[HXLibs] OpenSSL found, SSL support enabled.")
        if (CMAKE_PROJECT_NAME STREQUAL "HXLibs")
            add_compile_definitions(HXLIBS_ENABLE_SSL)
            link_libraries(OpenSSL::SSL OpenSSL::Crypto)
        else()
            target_compile_definitions(${hxlibs_target_name} INTERFACE HXLIBS_ENABLE_SSL)
            target_link_libraries(${hxlibs_target_name} INTERFACE OpenSSL::SSL OpenSSL::Crypto)
        endif()
    else()
        message(WARNING "[HXLibs] OpenSSL not found, SSL support will be disabled.")
    endif()
endif()

# db 模块
option(HXLIBS_ENABLE_DB "Enable DB support" ON)
message(STATUS "[HXLibs] HXLIBS_ENABLE_DB: ${HXLIBS_ENABLE_DB}")
if (HXLIBS_ENABLE_DB)
    # 用户是否启用 SQLite3 支持
    option(HXLIBS_ENABLE_DB_SQLITE3 "Enable SQLite3 support" OFF)
    message(STATUS "[HXLibs] HXLIBS_ENABLE_DB_SQLITE3: ${HXLIBS_ENABLE_DB_SQLITE3}")
    if (HXLIBS_ENABLE_DB_SQLITE3)
        find_package(SQLite3 QUIET)
        if (SQLite3_FOUND)
            message(STATUS "[HXLibs] SQLite3 found, SQLite3 support enabled.")
            if (CMAKE_PROJECT_NAME STREQUAL "HXLibs")
                add_compile_definitions(HXLIBS_ENABLE_DB_SQLITE3)
                link_libraries(SQLite::SQLite3)
            else()
                target_compile_definitions(${hxlibs_target_name} INTERFACE HXLIBS_ENABLE_DB_SQLITE3)
                target_link_libraries(${hxlibs_target_name} INTERFACE SQLite::SQLite3)
            endif()
        else()
            message(WARNING "[HXLibs] SQLite3 not found, SQLite3 support will be disabled.")
        endif()
    endif()
endif()